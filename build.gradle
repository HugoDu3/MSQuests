plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
}

group = 'com.github.ibanetchep'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")
    implementation("com.github.technicallycoded:FoliaLib:main-SNAPSHOT")
    implementation("dev.dejvokep:boosted-yaml:1.3.7")
    implementation("org.jdbi:jdbi3-core:3.47.1-SNAPSHOT")
    implementation("com.zaxxer:HikariCP:5.1.0")

    implementation("io.github.revxrsal:lamp.common:4.0.0-rc.9")
    implementation("io.github.revxrsal:lamp.bukkit:4.0.0-rc.9")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate "com.tcoded.folialib", "com.github.ibanetchep.msquests.lib.folialib"
    relocate("dev.dejvokep.boostedyaml", "com.github.ibanetchep.msquests.lib.boostedyaml")
}